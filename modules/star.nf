process STAR_INDEX {
    tag "$fasta"
    label 'process_high'
publishDir "${params.outdir}/star_index", mode: 'copy'

    container 'quay.io/biocontainers/star:2.7.10b--h6b7c446_1'

    input:
    path fasta
    path gtf

    output:
    path "star_index", emit: index

    script:
    """
    mkdir star_index
    STAR --runMode genomeGenerate \\
         --genomeDir star_index \\
         --genomeFastaFiles $fasta \\
         --sjdbGTFfile $gtf \\
         --runThreadN ${task.cpus}
    """
}

process STAR_ALIGN {
    tag "$sample_id"
    label 'process_high'
    container 'quay.io/biocontainers/star:2.7.10b--h6b7c446_1'

publishDir "${params.outdir}/star", mode: 'copy'

    input:
    path index
    tuple val(sample_id), path(reads)

    output:
    tuple val(sample_id), path("*.bam"), emit: bam
    path "*.Log.final.out"             , emit: log

    script:
    """
    STAR --genomeDir $index \\
         --readFilesIn $reads \\
         --readFilesCommand zcat \\
         --outFileNamePrefix ${sample_id}. \\
         --outSAMtype BAM SortedByCoordinate \\
         --runThreadN ${task.cpus}
    """
}
